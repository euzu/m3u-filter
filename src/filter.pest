WHITESPACE = _{ " " | "\t" }
field = { ^"group" | ^"title" | ^"name" | ^"url" }
and = { ^"and" }
or = { ^"or" }
not = { ^"not" }
regexp = @{ "\"" ~ ( "\\\"" | (!"\"" ~ ANY) )* ~ "\"" }
type_value = { ^"live" | ^"vod" | ^"series" }
type_comparison = { ^"type" ~ "=" ~ type_value }
field_comparison_value = _{ regexp }
field_comparison = { field ~ "~" ~ field_comparison_value }
comparison = { field_comparison | type_comparison }
bool_op = { and | or}
expr_group = { "(" ~ expr ~ ")" }
expr = {
  comparison ~ (bool_op ~ expr)*
  | expr_group ~ (bool_op ~ expr)*
  | not ~ expr ~ (bool_op ~ expr)*
}
stmt = { expr  ~ (bool_op ~ expr)* }
main = _{ SOI ~ stmt ~ EOI }

